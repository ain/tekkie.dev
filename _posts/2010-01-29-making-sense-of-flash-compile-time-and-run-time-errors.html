---
layout: post
title: Making sense of Flash compile-time and run-time errors
date: 2010-01-29 12:02:47.000000000 +01:00
type: post
categories:
- flash
tags:
- ActionScript3
- AS3
- debug
- errorhandling
- FlashDev
author:
  display_name: Ain Tohvri
  first_name: Ain
  last_name: Tohvri
  twitter: tekkie
excerpt: Every new version of ActionScript has had its impact on the compiler and error handling. Since ActionScript 1, that had loose typing and was suited well for small-scale scripting projects, things have changed considerably.
last_modified_at: 2021-04-13 23:26:00 +01:00
---
<p><img class="teaser-image--left" title="ActionScript" src="{{ site.baseurl }}/assets/64px-ActionScript_icon.png" alt="ActionScript" width="64" height="64" />Every new version of ActionScript has had its impact on the compiler and error handling. Since ActionScript 1, that had loose typing and was suited well for small-scale scripting projects, things have changed considerably.</p><!--more-->
<p>With ActionScript 2 Flash community first saw the compile-time type checking and class-based syntax which basically allowed Object-oriented programming approaches. Despite the class-based inheritance that took AS2 closer to Java for instance, compiling still took it to AS1 bytecode over legacy matters. So basically the class-based inheritance that allowed the <abbr title="Object-oriented programming">OOP</abbr> approach was a layer on top of the existing prototype-centric AS1.</p>
<p>With Flex 2 and ActionScript 3 the scripting language changed a lot. The whole thing was restructured and new AS3-specific virtual machine (<abbr title="ActionScript Virtual Machine 2">AVM2</abbr>) was introduced. For those working in Flash IDE prior this era, it changed a lot. With a "habit" of procedural programming they had to rethink it. On the new platform, even if compile-time errors made sense referring to the line error occurred at, run-time errors didn't really help them out much, e.g.:</p>
<pre lang="latex">TypeError: Error #1009: Cannot access a property or method of a null object reference.</pre>
<p>or slightly better</p>
<pre lang="latex">ReferenceError: Error #1069: Property property0 not found on Doc and there is no default value.</pre>
<p>but also without a line number. Something that can be understood at run-time, we're dealing with bytecode.</p>
<p>Nevertheless the situation is a bit similar to Java and you can actually quickly scope the above error using the call stack backtrace:</p>
<pre lang="latex">ReferenceError: Error #1069: Property property0 not found on Doc and there is no default value.
	at Doc/handleTimerResult()
	at flash.events::EventDispatcher/dispatchEventFunction()
	at flash.events::EventDispatcher/dispatchEvent()
	at flash.utils::Timer/tick()</pre>
<p>so it runs up to handleTimerResult and then gets stuck. A quick dive into the (illustrative) code reveals that the trace is trying to access property0 during the while iteration at line 51.</p>
<pre lang="actionscript" line="1">package {
	import flash.display.MovieClip;
	import flash.utils.Timer;
	import flash.events.TimerEvent;

	public class Doc extends MovieClip {

		public var property1:uint;
		public var property2:uint;
		private var timer:Timer;

		public function Doc() {
			init();
		}

		private function init():void {
			trace("Doc Class initilized!");
			setProperties();
			initTimer();
		}

		private function setProperties():void {
			setProperty1();
			setProperty2();
		}

		public function setProperty2():void {
			property2 = randomOf100();
			trace("2nd property set to "+property2);
		}

		public function setProperty1():void {
			property1 = randomOf100();
			trace("1st property set to "+property1);
		}

		private function randomOf100():uint {
			return Math.random() * 100;
		}

		private function initTimer():void {
			timer = new Timer(1000, 5);
			timer.addEventListener(TimerEvent.TIMER, handleTimer);
			timer.addEventListener(TimerEvent.TIMER_COMPLETE, handleTimerResult);
			timer.start();
		}

		private function handleTimerResult(event:TimerEvent):void {
			var i:int = 3;
			while (i--) {
				trace("Property "+i+": "+this['property'+i]); // run-time error
			}
		}

		private function handleTimer(event:TimerEvent):void {
			setProperty1();
			setProperty2();
			trace("Properties reset!");
		}
	}
}</pre>
<p>In most cases, if a class is structured reasonably, the above logic should deliver nicely. If this is not the case, in Flash IDE, instead of the regular Cmd/Ctrl + Enter "Test Movie" functionality, you also have "Debug Movie". It will launch standalone Flash Player and provide you with the line number in the backtrace:</p>
<pre lang="bash">ReferenceError: Error #1069: Property property0 not found on Doc and there is no default value.
	at Doc/handleTimerResult()[/Users/[...]/Doc.as:51]
	at flash.events::EventDispatcher/dispatchEventFunction()
	at flash.events::EventDispatcher/dispatchEvent()
	at flash.utils::Timer/tick()</pre>
<p>Related references:</p>
<ul>
<li><a href="https://help.adobe.com/en_US/FlashPlatform/reference/actionscript/3/index.html">ActionScript 3 documentation</a>
</ul>
